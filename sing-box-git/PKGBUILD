# Maintainer: everyx <lunt.luo@gmail.com>
# Contributor: BGME <i@bgme.me>
# Contributor: Sieve Lau <sievelau@gmail.com>

pkgname=sing-box-git
pkgver=1.3beta12.r0.g951d83c
pkgrel=1

pkgdesc='The universal proxy platform (git version).'
arch=('x86_64' 'i686')
url='https://sing-box.sagernet.org/'
license=('GPL3')

provides=("${pkgname%-git}")
conflicts=("sing-box" "sing-box-beta")

makedepends=('go')
optdepends=('sing-geosite:  sing-geosite database'
            'sing-geoip:    sing-geoip database')

backup=("etc/${pkgname%-git}/config.json")

source=("${pkgname%-git}::git+https://github.com/SagerNet/sing-box.git#branch=dev-next")
sha256sums=(SKIP)

pkgver() {
    cd "${pkgname%-git}"
    git describe --tags --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-\([^-]*\)-\([^-]*\)$/.\1.\2/;s/-//'
}

build(){
    cd "${pkgname%-git}"

    export CGO_CPPFLAGS="$CPPFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_LDFLAGS="$LDFLAGS"
    export VERSION=$(go run ./cmd/internal/read_tag)

    go build \
        -v \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "
            -X \"github.com/sagernet/sing-box/constant.Version=$VERSION\"
            -s -w -buildid= 
            -linkmode=external" \
        ./cmd/sing-box

    sed -i "/^\[Service\]$/a StateDirectory=${pkgname%-git}"    release/config/${pkgname%-git}.service
    sed -i "/^\[Service\]$/a StateDirectory=${pkgname%-git}-%i" release/config/${pkgname%-git}@.service
    sed -i "/^\[Service\]$/a User=${pkgname%-git}"              release/config/${pkgname%-git}*.service

    echo 'u sing-box - "Sing-box Service" - -' > "release/config/${pkgname%-git}.sysusers"
}

package() {
    cd "${pkgname%-git}"

    install -Dm644 LICENSE                             -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
    install -Dm755 "${pkgname%-git}"                         -t "$pkgdir/usr/bin"
    install -Dm644 "release/config/config.json"        -t "$pkgdir/etc/${pkgname%-git}"
    install -Dm644 "release/config/${pkgname%-git}.service"  -t "$pkgdir/usr/lib/systemd/system"
    install -Dm644 "release/config/${pkgname%-git}@.service" -t "$pkgdir/usr/lib/systemd/system"
    install -Dm644 "release/config/${pkgname%-git}.sysusers"    "$pkgdir/usr/lib/sysusers.d/${pkgname%-git}.conf"
}
